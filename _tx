complete_flags() {
    local -a flags
    flags=(
        "--help:show help message"
        "--ls:show running sessions and layouts"
        "--ls-sessions:list sessions only"
        "--ls-layouts:list layouts only"
        "--layout:create a new layout or edit existing one"
        "--preview:preview session or layout"
        "--picker:pick session or layout"
        "--sessions:open sessionizer"
        "--add-sessions:add a new directory to sessionizer"
        "--rm-sessions:remove a directory from sessionizer"
    )

    _describe 'flags' flags
}

complete_aliases() {
    local -a aliases
    aliases=(
       "-h:alias for --help"
       "ls:alias for --ls"
       "lss: alias for --ls-sessions"
       "lsl: alias for --ls-layouts"
       "-l:alias for --layout"
       "-p:alias for --preview"
       "-s:alias for --sessions"
       "-as:alias for --add-sessions"
       "-rs:alias for --rm-sessions"
    )

    _describe 'aliases' aliases
}

complete_sessions() {
    local -a sessions
    sessions=("${(@f)$(tx ls | grep "\(session\)" | awk '{print $1}')}")
    _describe 'sessions' sessions
}

complete_layouts() {
    local -a layouts
    layouts=("${(@f)$(tx ls | grep "\(layout\)" | awk '{print $1}')}")
    _describe 'layouts' layouts
}

complete_layouts_edit() {
    local -a layouts
    layouts=("${(@f)$(find "$TX_ROOT/sessions" -type f -name '*.layout.sh' -exec basename {} .layout.sh \;)}")
    _describe 'layouts' layouts
}

_tx() {
    if (( CURRENT == 3 )); then
        case "${words[2]}" in
            -l | --layout)
                complete_layouts_edit
                ;;
            *)
                complete_sessions
                complete_layouts
                ;;
        esac
    else
        complete_sessions
        complete_layouts
        complete_flags
        complete_aliases
    fi
}

compdef _tx tx
